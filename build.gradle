buildscript {
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
	}
}

ext."signing.keyId" = project.hasProperty("signing.keyId") ? project.getProperty('signing.keyId') : System.getenv('SIGNING_KEY_ID')
ext."signing.password" = project.hasProperty("signing.password") ? project.getProperty('signing.password') : System.getenv('SIGNING_PASSPHRASE')
ext."signing.secretKeyRingFile" = project.hasProperty("signing.secretKeyRingFile") ? project.getProperty('signing.secretKeyRingFile') : ("${System.properties['user.home']}${File.separator}.gnupg${File.separator}secring.gpg")
ext.isReleaseVersion = !projectVersion.endsWith("SNAPSHOT")

version project.projectVersion
group "org.grails.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "java-library"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-gsp"
apply plugin: "io.github.gradle-nexus.publish-plugin"
apply plugin: "maven-publish"
apply plugin: "signing"

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    documentation
}

dependencies {
	implementation "org.grails:grails-web-databinding"
	implementation "org.grails:grails-plugin-validation"
	implementation "org.grails:grails-datastore-core:$gormVersion"
	implementation "org.grails:grails-datastore-gorm:$gormVersion"
	implementation "org.grails:grails-web-gsp:$grailsVersion"
	implementation "org.grails:grails-scaffolding-core:$scaffoldingVersion"
	implementation "commons-lang:commons-lang:2.6"
	compileOnly "org.grails:grails-boot"
	compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"

    documentation "org.fusesource.jansi:jansi:1.14"
	documentation "com.github.javaparser:javaparser-core:3.15.14"

	testImplementation "org.grails:grails-test-support"
	testImplementation 'javassist:javassist:3.12.0.GA'
	testImplementation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
	testImplementation "cglib:cglib-nodep:$cglibNodepVersion"
	testImplementation("org.jodd:jodd-wot:$joddWotVersion") {
		exclude module: 'slf4j-api'
		exclude module: 'asm'
	}
}

groovydoc.doFirst {
    classpath += (configurations.documentation + configurations.compileClasspath)
}

bootJar.enabled = false

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

jar {
    enabled = true
    archiveClassifier.set('')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'fields'
            version = project.version

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            from components.java

            pom {
                name = "Grails Fields Plugin"
                description = "Customizable form-field rendering based on overrideable GSP template"
                url = 'https://github.com/rainboyan/grails-fields'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'rainboyan'
                        name = 'Michael Yan'
                        email = 'rain@rainboyan.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rainboyan/grails-fields.git'
                    developerConnection = 'scm:git:ssh://github.com:rainboyan/grails-fields.git'
                    url = 'https://github.com/rainboyan/grails-fields/tree/main'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            def ossUser = System.getenv("SONATYPE_USERNAME") ?: project.hasProperty("sonatypeOssUsername") ? project.sonatypeOssUsername : ''
            def ossPass = System.getenv("SONATYPE_PASSWORD") ?: project.hasProperty("sonatypeOssPassword") ? project.sonatypeOssPassword : ''
            def ossStagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID") ?: project.hasProperty("sonatypeOssStagingProfileId") ? project.sonatypeOssStagingProfileId : ''
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = ossUser
            password = ossPass
            stagingProfileId = ossStagingProfileId
        }
    }
}

afterEvaluate {
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.maven
    }
}
